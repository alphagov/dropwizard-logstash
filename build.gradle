plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

def dependencyVersions = [
        dropwizard: '1.1.0'
]

dependencies {
    compile "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard",
            'net.logstash.logback:logstash-logback-encoder:4.9'

    testCompile 'junit:junit:4.11',
            'org.hamcrest:hamcrest-library:1.3',
            'org.mockito:mockito-core:1.9.5',
            "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'uk.gov.ida'
            artifactId 'dropwizard-logstash'
            version "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "file:/srv/maven" // change to point to your repo, e.g. http://my.org/repo
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'dropwizard-logstash'
        userOrg = 'alphagov'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alphagov/dropwizard-logstash.git'
        version {
            name = "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
        }
    }
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
    module {
        //if you love browsing Javadoc
        downloadJavadoc = true

        //and hate reading sources :)
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}
