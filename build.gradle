plugins {
    id "com.jfrog.bintray" version "1.8.4"
}
apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

def dependencyVersions = [
        dropwizard: '2.0.2'
]

group 'uk.gov.ida'
version "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

dependencies {
    implementation "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard"

    api 'net.logstash.logback:logstash-logback-encoder:4.11',
            'ch.qos.logback:logback-access:1.2.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2',
            'org.hamcrest:hamcrest-library:2.2',
            'org.mockito:mockito-core:3.2.0',
            'com.github.stefanbirkner:system-rules:1.16.1',
            "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard"
}

sourceSets {
    java9 {
        if (JavaVersion.current() >= JavaVersion.VERSION_1_9) {
            java {
                srcDir 'src/main/java9'
            }
        }
    }
    java10 {
        if (JavaVersion.current() >= JavaVersion.VERSION_1_10) {
            java {
                srcDir 'src/main/java10'
            }
        }
    }
    java11 {
        if (JavaVersion.current() >= JavaVersion.VERSION_11) {
            java {
                srcDir 'src/main/java11'
            }
        }
    }
}

compileJava {
    if(JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        options.compilerArgs.addAll(['--release', '8'])
    }
}

compileJava9Java {
    if(JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        options.compilerArgs.addAll(['--release', '9'])
    }
}

compileJava10Java {
    if(JavaVersion.current() >= JavaVersion.VERSION_1_10) {
        options.compilerArgs.addAll(['--release', '10'])
    }
}

compileJava11Java {
    if(JavaVersion.current() >= JavaVersion.VERSION_11) {
        options.compilerArgs.addAll(['--release', '11'])
    }
}

test {
    testLogging {
        events "skipped", "failed", "standardError"
        showCauses true
        showExceptions true
        showStackTraces true
        exceptionFormat "full"

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava
}

jar {
    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }
    into('META-INF/versions/10') {
        from sourceSets.java10.output
    }
    into('META-INF/versions/11') {
        from sourceSets.java11.output
    }
    manifest {
        if(JavaVersion.current() >= JavaVersion.VERSION_1_9) {
            attributes.put("Multi-Release", "true")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven-test'
        name = 'dropwizard-logstash'
        userOrg = 'alphagov'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alphagov/dropwizard-logstash.git'
        version {
            name = "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
        }
    }
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
    module {
        //if you love browsing Javadoc
        downloadJavadoc = true

        //and hate reading sources :)
        downloadSources = true
    }
}
